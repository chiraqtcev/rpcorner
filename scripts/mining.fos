// Author: Anuri
#ifndef MINING
#define MINING

#include "_utils.fos"
#include "_ltp.fos"
#include "gathering_h.fos"
#include "critter_skills_h.fos"
#include "entire_h.fos"
#include "repair_h.fos"

// Ores array search
uint16 FindOre( Item@ targetItem )
{
    return FindOre( targetItem.GetProtoId() );
}

uint16 FindOre( uint16 pid )
{    
    int index = 0;
    
    for( uint i = 0, l = OreList.length(); i < l; i++ )
    {
        index = OreList[i].find( pid );
        if( index != -1 )
		{
            return ++ i;
		}
	}
    return 0;
}

// LTP (long time process)
bool ltp_mining_inited = false;
void ltp_mining_init()
{
	LTPREG( LTP_MINING, process_mining )
	ltp_mining_inited = true;
}
//exported to main.fos - checks before start
bool PrepareMining( Critter& cr, Item@ tool, Item@ target ) 
{
	Map@ map = cr.GetMap();
    if( !valid( map ) ) 
    {
    	return false;
    }
	if( !valid( tool ) || !valid(target) )
	{
		return false;
	}
   	uint toolPid = tool.GetProtoId();
	if( MiningTools.find( toolPid ) == -1 )
	{
		return false;
	}
	uint8 Ore = FindOre( target );
	if( Ore == 0 )
	{
		return false;
	}
	if( tool.CritSlot != SLOT_HAND1 )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_EQUIP_TOOL );
		return false;
	}

	if( IsTired( cr ) ) 
	{ 
		return false;
	}
	
	if( cr.IsInjured() )
	{ 
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_INJURED_CANT_WORK );
		return false; 
	}
	
	if( toolPid == PID_DRILL )
	{
		if( tool.AmmoCount < 1 )
		{
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_NO_DRILL_CHARGE );
			return false;
		}
		uint det = GetDeteriorationProcent(tool);
		if( det >= 95 )
		{
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_DRILL_DAMAGED );
			return false;
		}
		SetDeterioration( tool, det + 1 );
	}
	start_mining( cr, tool, target );
	return true;
}

bool start_mining( Critter& cr, Item& tool, Item& target )
{
	if(!ltp_mining_inited)
	{
		ltp_mining_init();
	}
	
	if( valid( tool ) && valid( target ) )
	{	
		uint hit_pause = ACTION_PAUSE_BASE - (cr.Stat[ST_AGILITY] * ACTION_PAUSE_BONUS_RATE );
		uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
		if( tool.GetProtoId() == PID_DRILL )
		{
			action_pause = 700;
			Map@ map = cr.GetMap();
			PlayGatheringSound( cr, map, "drill_mine.ogg", 100 );
		}
		else
		{
			uint[] values = { cr.Id, tool.Id };
			CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_MiningSound", values, false);
			CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", cr.Id, true);	
		}
		StartProcess( cr, LTP_MINING, 0, tool.Id, target.Id, action_pause );
		return true;
	}
	return false;
}

uint process_mining( Critter@ cr, int& param0, int& param1, int& param2 )
{
    LTPROCESS( cr, param0, LTP_MINING )
	uint hit_pause = ACTION_PAUSE_BASE - (cr.Stat[ST_AGILITY] * ACTION_PAUSE_BONUS_RATE );
	uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
    Item@ tool = GetItem( param1 );
  	Item@ target = GetItem( param2 );

	if( IsTired( cr ) ) 
    { 
        StopProcess( cr );
        return 0;
    }

  	if( valid( tool ) && valid( target ) )
    {
      	param0++;
		Item@ tool = cr.GetItem( 0, SLOT_HAND1 );
		if( valid( tool ) )
		{
			uint object_dpa = ( cr.Skill[ SK_MINING ] ) + ( cr.Stat[ST_STRENGTH] * 10 ) + OBJECT_DPA_BASE; 
			int sequence_length = ceil( float( OBJECT_HP_BASE / ( CLAMP( object_dpa, OBJECT_DPA_MIN, OBJECT_DPA_MAX ) ) ) );
			if ( tool.GetProtoId() == PID_DRILL )
			{
				sequence_length = 10;
			}
			
			if( param0 > sequence_length )
			{
				Profit( cr, tool, target );
				int skillNum = SK_MINING;
				Accident( cr, skillNum );
				param0 -= sequence_length;
			}
			
			if( tool.GetProtoId() == PID_DRILL )
			{
				_CritAnimateBrust( cr );
				action_pause = 700;
			
			}
			else
			{
				_CritAnimateSwing(cr);
				uint[] values = { cr.Id, tool.Id };
				CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_MiningSound", values, false );
			}

			CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", cr.Id, true );
			return action_pause;
		}
    }
	return 0;
}

uint e_MiningSound( uint[]@ values )
{
	Critter@ cr = GetCritter(values[0]);
	Map@ map = cr.GetMap();
	Item@ tool = GetItem(values[1]);
	uint toolPid = tool.GetProtoId();
	string sound = "";
	switch( toolPid )
	{
		case( PID_PRIMITIVE_SLEDGE ):
		case( PID_SLEDGEHAMMER ):
		case( PID_SUPER_SLEDGE ):
		case( PID_SLEDGEHAMMER2 ):
		case( PID_SM_HAMMER ):
		case( PID_SM_CLUB ):
			sound = _GetOneRandom( HammerSounds );
			break;
		case( PID_SHOVEL ):	
		case( PID_PICKAXE ):
			sound = _GetOneRandom( PickaxeSounds );
			break;
		default:
			break;
	}
	PlayGatheringSound( cr, map, sound, 30 );
	return 0;
}

bool Profit(  Critter& cr, Item@ tool, Item@ target )
{
	int amount = 0;
	uint toolMod = tool.GetProtoId();
	switch( toolMod )
	{	
		case( PID_SUPER_SLEDGE ):
		case( PID_PICKAXE ):
			amount++;
			break;
		case( PID_SHOVEL ):
		case( PID_SM_CLUB ):
			amount--;
			break;
		case( PID_PRIMITIVE_SLEDGE ):
			amount-= 2;
			break;
	}
	uint OreMod = target.GetProtoId();
	switch( OreMod )
	{	
		case( PID_CUPPER_ORE_S ):
		case( PID_LED_ORE_S ):
		case( PID_MINERALS_S ):
		case( PID_IRON_ORE_S ):
			amount--;
			break;
		case( PID_GOLD_ORE_S ):
		case( PID_URANIUM_ORE_S ):
			amount -= 2;
			break;
		case( PID_GOLD_ORE_M ):
		case( PID_URANIUM_ORE_M ):
			amount--;
			break;
	}
			
	uint lootRoll = ( Random( 0, 1 ) + ( cr.ParamBase[ SK_MINING ] / 50 ) );
	
	if( toolMod == PID_DRILL )
		amount = ( ( amount + lootRoll ) * CLAMP( 3, 1, ( target.RESOURCE_CAPACITY / OBJECT_HP_BASE ) ) );
	else
		amount += lootRoll;
	
	if( amount < 1 )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_NOTHING_USEFULL );
	}
	else
	{
		int skillNum = SK_MINING;
		raiseSkill( cr, skillNum );
		
		uint16 lootPid = 0;
		switch( target.ORE_TYPE )
		{
			case(0):
			{	
				if( Random( 1, 3 ) != 3 )
				{
					lootPid = PID_ROCK;
					amount = Random( 1, 3 );
				}
				else
				{
					lootPid = PID_FLINT;
					amount = 1;
				}
				break;
			}

			case(1):
			{
				lootPid = PID_COPPER_ORE;
				break;
			}

			case(2):
			{
				lootPid = PID_LEAD_ORE;
				break;
			}

			case(3):
			{
				lootPid = PID_MINERAL;
				break;
			}

			case(4):
			{
				lootPid = PID_COAL;
				break;
			}

			case(5): 
			{
				int randomval = Random(1, 100);

				if( randomval > 0 && randomval <= 45 )
					lootPid = PID_GOLD_SMALLORE;

				if( randomval > 45 && randomval <= 90 )
					lootPid = PID_GOLD_SAND;

				if( randomval > 90 && randomval <= 100 )
					lootPid = PID_GOLD_ORE;

				amount = CLAMP( amount - Random(3, 4), 1, amount ); 
				break;
			}

			case(6):
			{
				lootPid = PID_METAL_ORE;
				break;
			}

			case(7):
			{
				lootPid = PID_URANIUM_ORE;
				break;
			}
		}
		
		cr.AddItem( lootPid, amount );
		
		cr.RunClientScript( "client_main@__message_result", lootPid, amount, 0, null, null );
		
		cr.StatBase[ST_EXPERIENCE] += 25;
		
		if( toolMod == PID_DRILL )
		{	
			target.RESOURCE_CAPACITY = target.RESOURCE_CAPACITY - ( OBJECT_HP_BASE * 3 );	
			tool.AmmoCount--;
			tool.Update();
		}
		else
		{	
			target.RESOURCE_CAPACITY = target.RESOURCE_CAPACITY - OBJECT_HP_BASE;
		}
		
		if( target.RESOURCE_CAPACITY <= 0 && ( target.ORE_TYPE == 0 && target.RESOURCE_DNA != 0 ) )
		{
			Map@ map = GetMap( target.MapId );
			uint16 hx = target.HexX, hy = target.HexY;
			
			switch( OreMod )
			{
				case( PID_ORE_DEPOSIT_S ):
				case( PID_ORE_DEPOSIT_S2 ):
					map.AddItem( hx, hy, OreList[target.RESOURCE_DNA][ OreList[target.RESOURCE_DNA].length() - 3 ], 1 );
					break;
				case( PID_ORE_DEPOSIT_M ):
					map.AddItem( hx, hy, OreList[target.RESOURCE_DNA][ Random( 0, OreList[target.RESOURCE_DNA].length() - 2 ) ], 1 );
					break;
				case( PID_ORE_DEPOSIT_L ):
					map.AddItem( hx, hy, OreList[target.RESOURCE_DNA][ Random( 1, OreList[target.RESOURCE_DNA].length() - 1 ) ], 1 );
					break;
			}
			
			DeleteItem( target );
			return true;
		}
		
		if( target.RESOURCE_CAPACITY <= 0 )
		{
			DeleteItem( target );
			return true;
		}
	}
	return true;
}
//===============ORE SPAWNER MECHANIC===============//

void OreAutoInit() //export to gameplay.fos
{    
    Map@[] maps;
    for( int i = 0, iend = GetAllMaps( 0, maps ); i < iend; i++)
	{   
		CheckMapEvent( maps[i] );
	}
}

uint e_SpawnOres( int[] @ values )
{
	Map @ map = GetMap( values[ 0 ] );
	if( !valid( map ) )
	{	
		return 0;
	}
	
	SpawnOre( map );

	EraseTimeEvent( map.GetData( MAP_TIME_DATA_MINING ) );
	timeNumb = CreateTimeEvent( AFTER( DELAY_FOR_HARVESTABLE_LOOT_RESPAWN ), "e_SpawnOres", values, true );
	map.SetData( MAP_TIME_DATA_MINING, timeNumb );
	return 0;
}

bool SpawnOre( Map& map )
{
	for( int i = 0; i < ORE_SPAWNER_COUNT; i++ )
	{
		int entireId = ORE_ENTIRE + i;
		int entires = map.CountEntire( entireId );
		//Log("" + entires);
		uint16 hx=0, hy=0;
		for( int j = 0; j < entires; j++ )
		{
			map.GetEntireCoords( entireId, j, hx, hy );
			if( map.IsHexPassed( hx, hy ) )
			{	
				Item@ BaseOre = map.AddItem( hx, hy, OreList[0][ Random( 0, OreList[0].length() - 2 ) ], 1 );
				BaseOre.RESOURCE_DNA = ( entireId - 1000 );
			}
		}
	}
	return true;
}

//Forced Spawn
void _OresInit(Critter& player, int param0, int param1, int param2)
{
	ForceSpawnOres();
}

//import void ForceSpawnOres() from "mining";
void ForceSpawnOres()
{
    Map@[] maps;
    for( int i = 0, iend = GetAllMaps( 0, maps ); i < iend; i++)
	{   
		CheckMapEvent( maps[i] );
	}
}

bool CheckMapEvent( Map& map )
{
    for( int n = ORE_ENTIRE, nend = ORE_ENTIRE  + ORE_SPAWNER_COUNT; n < nend ; n++ )
	{
		if( map.CountEntire( n ) != 0 ) 
		{
			int[] values = {map.Id};
			map.SetData( MAP_TIME_DATA_MINING, CreateTimeEvent( AFTER( REAL_SECOND( 1 ) ), "e_SpawnOres", values, true ) );
			//Log("ore_event_started");
			return true;
		}
	} 
    return false;
}

#endif //MINING