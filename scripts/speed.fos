#ifndef SPEED
#define SPEED

#include "_utils.fos"
#include "_starter.fos"
#include "speed_h.fos"

START( "speed", "InitSpeedArray" );

int[] SpeedArray( 2000, 0 );

void InitSpeedArray() // Export
{
    file crTypes;
    if( crTypes.open( "./data/CritterTypes.cfg", "r" ) < 0 )
        return;

    int i = 0;
	while( !crTypes.isEndOfFile() )
    {
        string str;
        crTypes.readLine( str );
        if( str.length() < 10 || str[ 0 ] != '@' )
            continue;

		string@[] @ line = splitExSpaces( str );
		
		i++;
		//DebugLog( "#" + i + ") " + enlistSTR( line ) );

        int num = 0, walk = 0, run = 0;
        if( !StrToInt( line[ 1 ], num ) || num < 0 || num > 999 || !StrToInt( line[ 37 ], walk ) || !StrToInt( line[ 38 ], run ) )
            continue;

        SpeedArray[ num * 2 ] = walk;
        SpeedArray[ num * 2 + 1 ] = run;
    }
    crTypes.close();
}

void reset()
{
    Critter@[] crs;
    GetAllPlayers( crs );

    for( uint i = 0, j = crs.length(); i < j; i++ )
    {
        ChangeCritterSpeed( crs[ i ] );
    }
}

void reset( Critter& cr, int p0, int p1, int )
{
    reset();
}

void testISA( Critter& cr, int p0, int p1, int )
{
    InitSpeedArray();
    reset();
}

void ChangeCritterSpeed( Critter& cr )
{
	if( cr.ParamBase[ CR_FIXED_SPEED ] != 0 ) return;
	
	if( isGM( cr ) ) //Если не фиксированная скорость, и ты ГМ - ты не страдаешь от перегруза.
	{
		cr.ModeBase[ MODE_NO_RUN ] = 0;
		cr.ModeBase[ MODE_NO_WALK ] = 0
		;
		cr.StatBase[ ST_WALK_TIME ] = 400;
		cr.StatBase[ ST_RUN_TIME ] = 200;
		return;
	}
	
	if( cr.ParamBase[ CR_SLEEPING_STATE ] != 0 )
	{
		cr.ModeBase[ MODE_NO_WALK ] = 1;
		cr.ModeBase[ MODE_NO_RUN ] = 1;
		return;
	}

	uint16 run = 0, walk = 0;
    float    curHp = cr.Stat[ ST_CURRENT_HP ], maxHp = cr.Stat[ ST_MAX_LIFE ];
    float   carry = cr.Stat[ ST_CARRY_WEIGHT ], weight = ItemsWeight( cr ), carryMod = 0;

    Item @ armor = cr.GetItem( 0, SLOT_ARMOR );
    bool apa = false;
    if( @armor != null )
    {
        uint16 pid = armor.GetProtoId();

        apa = ( pid == PID_POWERED_ARMOR ||
                pid == PID_HARDENED_POWER_ARMOR ||
                pid == PID_ADVANCED_POWER_ARMOR ||
                pid == PID_ADVANCED_POWER_ARMOR_MK2 );
    }

    if( carry == 0 )
        carry += 1;
    float weight_cantRun = carry * 0.71,
         weight_limitWalk = carry,
         weight_cantWalk = carry * 1.5;

    bool CanRun = ( SpeedArray[ cr.CrType * 2 + 1 ] != 0 ) && ( weight < weight_cantRun ) /*&& !apa */;

	Item@ shackles = cr.GetItem( 0, SLOT_MISC );
	if( valid( shackles ) && shackles.GetProtoId() == PID_SHACKLES && shackles.Val3 == 0 && shackles.Val2 != 0 )
		CanRun = false;

    cr.ModeBase[ MODE_NO_RUN ] = ( CanRun ? 0 : 1 );
	
	if( cr.GetTimeEvents( CTE_SLOWRELOAD, null, null, null ) > 0 )
		cr.ModeBase[ MODE_NO_RUN ] = 2;
	
    if( curHp <= 0 )
        curHp = 1;
	if( curHp >= maxHp * 0.25 )
		curHp = maxHp * 0.25;
    float hpPERCENT = curHp * 1.0 / ( maxHp * 0.25 );
	float healthMod = 2.0 - hpPERCENT;

    walk = uint16( SpeedArray[ cr.CrType * 2 ] );
    run  = uint16( SpeedArray[ cr.CrType * 2 + 1 ] );
	
	float dragging = FurnitureDraggingSpeedMod( cr );
	
	_testInfo( cr, CR_TEST_MODE_SPEED, SAY_NETMSG, "w " + walk + " r " + run + " h " + healthMod + " d " + dragging );
	
	cr.ModeBase[ MODE_NO_WALK ] = weight > weight_cantWalk ? 1 : 0;
	
	walk = CLAMP( walk * healthMod * dragging, 400, 1000 );
    run = CLAMP( run * healthMod * dragging, 200, walk );

	if( cr.ParamBase[ CR_YOU_DRAG ] != 0 ) {
		Critter@ target = GetCritter( cr.ParamBase[ CR_YOU_DRAG ] );
		if( valid( target ) ) {
			uint speedMod;
			int MobType = target.Stat[ ST_BASE_CRTYPE ];
			switch( MobType )
			{			
				case( CRTYPE_GIANT_BEETLE ):
				case( CRTYPE_GIANT_WASP ):
				case( CRTYPE_RAT ):
				case( CRTYPE_MANTI ):
				case( CRTYPE_DOG_SMALL ):
				case( CRTYPE_GIANT_ANT_BROWN ):
				case( CRTYPE_GIANT_ANT_BROWN_L ):
				case( CRTYPE_GIANT_ANT_GREEN ):
				case( CRTYPE_GIANT_ANT_RED ):
				case( CRTYPE_PLANT ):
				case( CRTYPE_ALIEN_EGG ):
					speedMod = 800;
					break;
				case( CRTYPE_GIANT_ANT_LION ):
				case( CRTYPE_RAT_PIGRAT ):
				case( CRTYPE_GECKO_GOLD_SMALL ):
				case( CRTYPE_DOG ):
				case( CRTYPE_DOG_BLC ):
				case( CRTYPE_DOG_BRN ):
					speedMod = 900;
					break;
				case( CRTYPE_GECKO_SILV ):
				case( CRTYPE_GECKO_GOLD ):
				case( CRTYPE_GECKO_CAVE ):
				case( CRTYPE_GECKO_FIRE ):
				case( CRTYPE_RADSCORPION ):
					speedMod = 1000;
					break;
				case( CRTYPE_RADSCORPION_HUGE ):
				case( CRTYPE_RADSCORPION_BLC_HUGE ):
				case( CRTYPE_RADSCORPION_CAVE ):
					speedMod = 1200;
					break;
				case( CRTYPE_RAT_MOLERAT ):
				case( CRTYPE_RAT_MOLERAT_MUTANT ):
				case( CRTYPE_BRAHMIN ):
				case( CRTYPE_ALIEN_REG ):
				case( CRTYPE_ALIEN_QUEEN ):
				case( CRTYPE_DEADCLAW_SMALL ):
				case( CRTYPE_FLOATER ):
					speedMod = 1300;
					break;
				case( CRTYPE_YAO ):
				case( CRTYPE_DEADCLAW ):
				case( CRTYPE_DEADCLAW_GREY ):
				case( CRTYPE_CROCS_BEETLE ):
				case( CRTYPE_CROCS_SHADOW ):
				case( CRTYPE_DEADCLAW_BEAST ):
				case( CRTYPE_CENTAUR ):
				case( CRTYPE_SWAMP_LURKER ):
					speedMod = 1500;
					break;
				default:
					speedMod = 1200;
					break;
			}
			int newSpeed = speedMod - ( cr.Stat[ ST_STRENGTH ] * 100 ); 
			walk += CLAMP( newSpeed, 0, 1500 );
			run = walk;
		}
	}

    cr.StatBase[ ST_WALK_TIME ] = walk;
    cr.StatBase[ ST_RUN_TIME ] = run;

	_testInfo( cr, CR_TEST_MODE_SPEED, SAY_NETMSG, "w " + cr.StatBase[ ST_WALK_TIME ] + " r " + cr.StatBase[ ST_RUN_TIME ] );
}

float FurnitureDraggingSpeedMod( Critter& cr )
{
	if( cr.GetTimeEvents( CTE_FURNITURE_DRAGGING, null, null, null ) == 0 )
		return 1;
	
	Item@ furniture = GetItem( cr.StatBase[ ST_LAST_CONT_ID ] );
	if( !valid( furniture ) ) 
		return 1;
	
	float furniture_weight = ItemsWeight( furniture );
	float max_weight = cr.Stat[ ST_CARRY_WEIGHT ] * ContainerBonusWeight( furniture );
	float min_weight = max_weight / 2;
	
	float weight_capacity = max_weight - min_weight;
	float weight_used = furniture_weight - min_weight;

	float result = 2 - ( weight_capacity - weight_used ) / weight_capacity;
	
	if( result < 1 )
		result = 1;
	
	if( result > 2 )
		result = 2;
	
	_testInfo( cr, CR_TEST_MODE_SPEED, SAY_NETMSG, "result " + result + " max_weight " + max_weight + " furniture_weight " + furniture_weight );
	_testInfo( cr, CR_TEST_MODE_SPEED, SAY_NETMSG, "weight_capacity " + weight_capacity + " weight_used " + weight_used );
	
	return result;
}

#endif //SPEED