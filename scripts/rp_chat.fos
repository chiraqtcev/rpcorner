#include "_utils.fos"

bool sinfFLAG( Critter& player, uint flag )
{
	return FLAG( player.Param[ CR_SINF_MODE ], flag );
}

//Отменяет или создает связь (следование) между существами. Нужно для визуального отображения стрелочек над существами.
bool unbound( Critter& owner, int targetID ) { return bound( owner, targetID, false ); }
bool bound( Critter& owner, int targetID, bool set = true )
{
	//"Пустой" вызов ф-ии.
	if( targetID == 0 ) return true;
	
	Critter@ target;
	if( !Find( target, targetID ) || !valid( target ) )
		return testInfo( owner, "Invalid target id: " + targetID );

	target.StatBase[ ST_FOLLOW_CRIT ] = ( set ? int( owner.Id ) : 0 );
	return true;
}

///Основная логика модуля.

//RunServerScriptUnsafe( "rp_chat@unsafe_SetChatTarget", targetId, 0, 0, null, null );
void unsafe_SetChatTarget( Critter& cr, int id, int, int, string@, int[]@ )
{
	if( !SetChatTarget( cr, uint( id ) ) )
		testInfo( cr, "Failed to set chat target: " + cr.Id + " " + id );
}

// Движковый клиент-чат отправляет не более 100 символов, процедура нужна чтобы обойти это ограничение
void unsafe_SayRemote(Critter& cr, int sayType, int, int, string@ message, int[]@) 
{
	cr.Say(sayType, message);
}

//Устанавливает новую цель для "ролевых возможностей чата": серого текста или речи за целевое существо.
//Используемая "обратная связь" отвечает за возможность говорить за ЛЮБЫХ животных у ГМов, и СВОИХ животных у обычных игроков.
bool SetChatTarget( Critter& cr, uint id )
{	
	cr.ParamBase[ CR_TALKING_TARGET_ID ] = id;
	
	if( isGM( cr ) && cr.Id != id ) //Если ГМ и не сам себя.
	{
		//Отменяет обратную связь со старым существом.
		unbound( cr, cr.StatBase[ ST_FOLLOW_CRIT ] );

		//Создает обратную связь с новым существом.
		bound( cr, cr.ParamBase[ CR_TALKING_TARGET_ID ] );
	}

	//Для визульного отображения - стрелочки над целью.
	cr.StatBase[ ST_FOLLOW_CRIT ] = cr.ParamBase[ CR_TALKING_TARGET_ID ];
	
	return true;
}

//RunServerScriptUnsafe( "rp_chat@unsafe_SayChatMessage", sayType, hexX, hexY, message, null );
void unsafe_SayChatMessage( Critter& cr, int sayType, int x, int y, string@ message, int[]@ )
{
	if( cr.IsDead() ) { return; }

	if( !SayChatMessage( cr, sayType, x, y, message ) )
		testInfo( cr, "Failed to say chat message: " + cr.Id + " " + sayType + " " + x + " " + y + " '" + message + "'" );
}

bool SayChatMessage( Critter& cr, int sayType, int x, int y, string@ message )
{
	bool isNear = GetDistantion( x, y, cr.HexX, cr.HexY ) < 6;

	//TODO: debug only, remove after successfull public tests
	testInfo( cr, 	( isGM( cr ) ? "" : "!" ) + "GM " + 
					cr.ParamBase[ CR_TALKING_TARGET_ID ] + " " + cr.StatBase[ ST_FOLLOW_CRIT ] + " " + cr.Stat[ ST_LEVEL ] + 
					" [" + x + ", " + y + " ] [" + cr.HexX + ", " + cr.HexY + " ] " + 
					( isNear ? "" : "!" ) + "Near" );
	
	if( sayType != SAY_SOCIAL && ( cr.StatBase[ ST_FOLLOW_CRIT ] == 0 || cr.StatBase[ ST_FOLLOW_CRIT ] != cr.ParamBase[ CR_TALKING_TARGET_ID ] ) )
	{	//Если это не "эмбиент"-сообщение (серый текст), нет цели или "сбилась" цель - ведём стандартную речь.
		cr.Say( sayType, message );
		SayLog( cr, sayType, message );
		return false;
	}
	
	if( sayType == SAY_SOCIAL || cr.StatBase[ ST_FOLLOW_CRIT ] == int( cr.Id ) ) //Серый текст.
	{
		if( !isGM( cr ) )
		{
			if( cr.Stat[ ST_LEVEL ] < 12 )//Карма-перк совать сюда.
			{
				cr.Say( SAY_NETMSG, "Доступно с 12го уровня." );
				return false;
			}			
			if( !isNear )
			{
				if( sayType == SAY_SOCIAL )
					cr.Say( SAY_NETMSG, "Курсор слишком далеко, предел 5 гексов." );
				else
				{
					cr.Say( sayType, message );
					SayLog( cr, sayType, message );
				}				
				
				return false;
			}
		}
		
		Map@ map = cr.GetMap();
		if( !valid( map ) ) return false;
		map.SetText( x, y, isGM( cr ) ? COLOR_GRAY : COLOR_LGRAY, message );
		SayLog( map, crInfo( cr, !isGM( cr ) ) + " :" + message + ":", x, y );
	}
	else
	{
		Critter@ target;
		if( !Find( target, cr.StatBase[ ST_FOLLOW_CRIT ] ) || target.IsDead() || target.StatBase[ ST_FOLLOW_CRIT ] != int( cr.Id ) || ( !isGM( cr ) && target.IsPlayer() ) ) 
		{ //Цель не найдена, не "зацеплена" или мертва - не сможет говорить. Дополнительное исключение - для защиты от разговоров "за зацепленных за себя" персонажей, если ты не ГМ.
			cr.Say( sayType, message );
			SayLog( cr, sayType, message );
			return false;
		}		
		
		if( target.Stat[ ST_CURRENT_HP ] < 0 || target.IsKnockout() )
		{
			Map@ map = target.GetMap();
			if( !valid( map ) ) return false;
			//map.SetText( target.HexX, target.HexY, COLOR_TEXT, "|0x00000000 :" + target.Id + ":|0xf9fa94 " + message );
			map.SetText( target.HexX, target.HexY, COLOR_TEXT, "|0xf9fa94 " + message );//TODO: Вынести отображение текста в клиентские скрипты, а-ля rp_combat.fos
		}
		else
		{
			// NPC cant use SHOUT for some unknown
			if (sayType == SAY_SHOUT || sayType == SAY_SHOUT_ON_HEAD) {
				if (sayType == SAY_SHOUT) sayType = SAY_NORM;
				else if (sayType == SAY_SHOUT_ON_HEAD) sayType = SAY_NORM_ON_HEAD;
				//message = "!!" + message;
			}
			target.Say( sayType, message );
		}

		if( isGM( cr ) )
			SayLog( target, sayType, message, "{ " + crInfo( cr ) + " } " );
		else
			SayLog( target, sayType, message, "[ " + crInfo( cr ) + " ] " );
	}
	
	return true;
}

void char_replace(string& text, uint8 from, uint8 to) 
{
	for(uint i=0, len=text.length(); i<len; i++) 
	{
		if (text[i] == from) 
		{
			text[i] = to;
		}
	}
}

uint8 backtick = "`"[0];
uint8 apostrophe = "'"[0];


void SendRadioLogs( Critter& cr, string@ message, int channel )
{
    if( !valid( message ) )
        return;

    uint len = message.length();
    if( len == 0 || len > 128 ) {
        return;
    }

    file f;
    string@ text = "empty",
            file_path = "empty",
            channel_name = "empty";

    if( channel == 0 )
    {
        file_path = "logs\\radio_0.txt";
        text = "`Рация 0> .."+message+"..`";
        channel_name = "нулевой_радиоканал";
    }
    else
    {
        file_path = "logs\\radio.txt";
        text = "`Рация " + channel + " | " + crInfo( cr ) + ": > .." + message + "..`";
        channel_name = "gm-radio";
    }

    if( text == "empty" || file_path == "empty" || channel_name == "empty" )
    	return;

    //Log("radio > " + file_path + " > " + text + " > " + channel_name );
    if( f.open(file_path, "a") < 0 ) 
        return;
    
    f.writeString(message + "\n");
    f.close();

    char_replace(message, backtick, apostrophe);
    DiscordSendMessage(channel_name, text);
    return;
}

void unsafe_GM_Hear_Radio( Critter& cr, int, int, int, string@ message, int[]@) //TODO: Причесать код, расширить функциональность.
{
	int[] channels;
	bool ch0 = false;
	Item@[] items;
	if( cr.GetItems( -1, items ) > 0 ) 
	{
		for( uint i = 0, l = items.length; i < l; i++ ) 
		{
			if( FLAG( items[i].Flags, ITEM_RADIO ) && !FLAG( items[i].RadioFlags, RADIO_DISABLE_SEND ) ) 
			{
				channels.insertLast( int( items[i].RadioChannel ) );
				SendRadioLogs( cr, message, int( items[i].RadioChannel ) );
			}
		}
	}
	
    Critter@[] crs;
    GetAllPlayers( crs );

    string str = enlistINT( channels ) + " " + GetPlayerName( cr.Id ) + ": |0xFFFFFFFF ..." + message +  "...";

    for( uint i = 0, j = crs.length(); i < j; i++ ) 
    {
        if( !valid( crs[i] ) || !isGM( crs[i] ) || !sinfFLAG( crs[i], SINF_RADIO ) ) 
        {
            continue;
		}
		if( crs[i].Id == cr.Id ) 
		{
			continue;
		}
        crs[ i ].Say( SAY_NETMSG, str );
    }
}

void unsafe_GM_Hear_Whisper( Critter& cr, int, int, int, string@ message, int[]@ ) //TODO: Причесать код.
{
    Critter@[] crs;
    GetAllPlayers( crs );
	
	Map @map = cr.GetMap();
	if(!valid(map)) return;

    for( uint i = 0, j = crs.length(); i < j; i++ )
    {
        if( !valid( crs[i] ) || !isGM( crs[i] ) || !sinfFLAG( crs[i], SINF_WHISPER ) )
            continue;
		if( crs[i].Id == cr.Id )
			continue;
		
		Map@ map1 = crs[i].GetMap();
		if( !valid(map1) || map1.Id != map.Id ) continue;
		
		if( GetDistantion( cr.HexX, cr.HexY, crs[i].HexX, crs[i].HexY ) <= __WisperDist )
			continue;
		
		crs[ i ].RunClientScript( "client_main@__HearWhisper", cr.HexX, cr.HexY, 0, GetPlayerName(cr.Id) + "@" + message, null );
    }
}

void unsafe_shout_all( Critter& cr, int, int, int, string@ message, int[]@ )//TODO: Причесать код.
{
	uint dist = ( isGM( cr ) ? 999 : cr.Stat[ ST_STRENGTH ] * 10 );

	Map@ map = cr.GetMap();
	if(!valid(map)) return;
	
	Critter@[] crs;
	uint count = map.GetCrittersHex(cr.HexX, cr.HexY, dist, FIND_ALL, crs);

    Critter@[] seeing;
	Critter@[] source = { cr };
	uint count2 = map.GetCrittersSeeing( source, true, FIND_ALL | FIND_ONLY_PLAYERS, seeing );

	for( uint i = 0; i < count; i++ )
	{
		bool isSkipping = false;
		for( uint j = 0; j < count2; j++ )
			if( crs[i].Id == seeing[j].Id )
			{
				isSkipping = true;
				break;
			}
		if( isSkipping )
			continue;
		
		uint dist = GetDistantion( cr.HexX, cr.HexY, crs[i].HexX, crs[i].HexY );
		if( int( dist ) <= crs[i].Stat[ ST_PERCEPTION ] * 10 )
			crs[i].Say( SAY_NETMSG, "???: |0x00FF0000 !!! " + strupr( message ) + " !!!" );
	}
}

void unsafe_SayLog( Critter& cr, int sayType, int, int, string@ message, int[]@ )
{
	if( SayLog( cr, sayType, message ) == "" )
		testInfo( cr, "Failed to log text: #" + cr.Id + " " + sayType + " '" + message + "'" );
}

void unsafe_stopTyping( Critter& cr, int, int, int, string@, int[]@ )
{
	StopTyping(cr);
}

void unsafe_ShowTyping( Critter& cr, int, int, int, string@, int[]@ )
{
	ShowTyping(cr);	
}

void ShowTyping( Critter& cr )
{
	if( isGM( cr ) && cr.ParamBase[ QST_INVIS ] != 0 ) return;
	if( FLAG( cr.Param[ MIO_FLAGS ], ITEM_NO_BLOCK ) ) { return; }

	Critter@ target = @cr;
	if( cr.StatBase[ ST_FOLLOW_CRIT ] != 0 && cr.ParamBase[ CR_TALKING_TARGET_ID ] == cr.StatBase[ ST_FOLLOW_CRIT ] )
		@target = GetCritter( cr.StatBase[ ST_FOLLOW_CRIT ] );
	
	if( !valid(target) || target.StatBase[ ST_FOLLOW_CRIT ] != int( cr.Id ) )
		@target = @cr;
	
	target.ParamBase[CR_TYPING_TIME] = __FullSecond + __TypingDelay * __TimeMultiplier;
	//if( target.GetTimeEvents( CTE_TYPING, null, null, null ) == 0 )
	//	target.AddTimeEvent( "cte_ShowTyping", REAL_SECOND( 5 ), CTE_TYPING, 0 );
}

void StopTyping( Critter& cr )
{
	if( isGM( cr ) && cr.ParamBase[ QST_INVIS ] != 0 ) return;

	Critter@ target = @cr;
	if( cr.StatBase[ ST_FOLLOW_CRIT ] != 0 && cr.ParamBase[ CR_TALKING_TARGET_ID ] == cr.StatBase[ ST_FOLLOW_CRIT ] )
		@target = GetCritter( cr.StatBase[ ST_FOLLOW_CRIT ] );

	if( !valid(target) || target.StatBase[ ST_FOLLOW_CRIT ] != int( cr.Id ) )
		@target = @cr;
	
	target.ParamBase[CR_TYPING_TIME] = 0;
	//target.EraseTimeEvents( CTE_TYPING );
}
/*
uint cte_ShowTyping( Critter& cr, int identifier, uint& rate ) 
{
	if( isGM( cr ) && cr.ParamBase[ QST_INVIS ] != 0 ) return 0;

	if( cr.ParamBase[CR_TYPING_TIME] > 0 )
	{
		string text = "|0xFF828a96 ";
		for( uint i = 0; i <= rate % 3; i++ )
			text += " .";
		rate++;
		
		Map@ map = cr.GetMap();
		if( !valid(map) )
			cr.Say( SAY_NORM_ON_HEAD, text );
		else
			map.SetText( cr.HexX, cr.HexY, COLOR_SAND, text ); //Нужно написать модуль, который отображал текст над именем криттера всем кто его видит, плюс учитывал дистанцию и рассеивание звуков.

		cr.ParamBase[CR_TYPING_TIME]--;
		return REAL_MS(500);
	}
	return 0;
}
*/