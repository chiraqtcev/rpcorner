/*********Графики и интерфейсы**************/

void DrawText( string@ text, Dot@ dot, int dx, int dy ) 
{ 
	if( !valid( text ) || !valid( dot ) ) return; 
	DrawText( text, dot.x + dx, dot.y + dy, dot.color ); 
}

void DrawText( string@ text, int x, int y, Color@ color )
{
	if( !valid( text ) ) return;
	
	DrawText( text, x, y, text.length() * 10, 20, valid( color ) ? color.rgb() : COLOR_RGB( 255, 0, 0 ), FONT_FALLOUT, 0 );
}

uint16 physical_selected_type = 0;
uint physical_selected_id = 0;
void PhysicalDrawTextAboveHex( string@ text, int hexX, int hexY, Color@ color, uint16 type, uint id, uint16 part = 0, bool show_line = false, bool show_rect = false) {
	if( !valid( text ) ) return;

	int anchor_x = 0, anchor_y = 0;
	GetHexPos( hexX, hexY, anchor_x, anchor_y );

	int width = 0;
	int height = 0;
	int lines = 0;
	int flags = FT_BORDERED | FT_CENTERX | FT_CENTERY;
	GetTextInfo( text, 0, 0, FONT_FALLOUT, flags, width, height, lines );
	width += 4;
	height += 4;

	int half_width = width/2;
	int half_height = height/2;

	int current_center_x = 0, current_center_y = 0;
	PhysicalUI_UpsertCustom(id, type, part, anchor_x, anchor_y, width, height, current_center_x, current_center_y);
	
	int current_topleft_x = current_center_x - half_width;
	int current_topleft_y = current_center_y - half_height;
	int current_bottomright_x = current_center_x + half_width;
	int current_bottomright_y = current_center_y + half_height;

	if( current_bottomright_x < 0 || current_bottomright_y < 0 || current_topleft_x > __ScreenWidth || current_topleft_y > __ScreenHeight ) {
		return;
	}

	if( __MouseX > current_topleft_x && __MouseY > current_topleft_y && __MouseX < current_bottomright_x && __MouseY < current_bottomright_y ) {
		physical_selected_id = id;
		physical_selected_type = type;
	}

	if( show_rect ) {
		rect_solid(current_topleft_x, current_topleft_y, width, height, color, 0.7);
	}

	if( show_line ) {
		draw_line_from_border(current_center_x, current_center_y, anchor_x, anchor_y, half_width, half_height, color);
	}
	
	DrawText( text, current_topleft_x, current_topleft_y, width, height, valid( color ) ? color.rgb() : COLOR_RGB( 255, 0, 0 ), FONT_FALLOUT, flags );
}

ItemCl@ PhysicalSelectItem(uint16 type) {
	if(physical_selected_type == type) {
		return GetItem(physical_selected_id);
	}
	return null;
}

void draw_line_from_border(int current_center_x, int current_center_y, int anchor_x, int anchor_y, int half_width, int half_height, Color& color) {
	int diff_x = current_center_x - anchor_x;
	int diff_y = current_center_y - anchor_y;
	int abs_diff_x = ABS(diff_x);
	int abs_diff_y = ABS(diff_y);
	if( abs_diff_x > half_width+5 || abs_diff_y > half_height+5 ) {
		int border_x = current_center_x;
		int border_y = current_center_y;
		if( abs_diff_x > abs_diff_y ) {
			if(diff_x > 0) {
				border_x -= half_width;
			} else {
				border_x += half_width;
			}
		} else {
			if(diff_y > 0) {
				border_y -= half_height;
			} else {
				border_y += half_height;
			}
		}
		line(border_x, border_y, anchor_x, anchor_y, color);
	}
}

void DrawTextAboveHex( string@ text, int hexX, int hexY, Color@ color, bool underline = false )
{
	if( !valid( text ) ) return;
	
	int x = 0, y = 0;
	GetHexPos( hexX, hexY, x, y ); 
	
	//string@[] lines;
	//lines = split( text, "\n" );
	
	//for( uint i = 0, l = lines.length(); i < l; i++ )
	//	DrawTextLine( lines[i], x, y + 10 - ( l - i ) * 20, color, underline );
	DrawTextLine( text, x, y + 10, color, underline );
}

void DrawTextLine( string@ text, int x, int y, Color@ color, bool underline = false )
{
	if( !valid( text ) ) return;
	
	int width = 0;
	int height = 0;
	int lines = 0;
	int flags = FT_BORDERED | FT_CENTERX;
	GetTextInfo( text, 0, 0, FONT_FALLOUT, flags, width, height, lines );

	int x0 = x - width / 2;
	int y0 = y - 10;

	//rect(x0, y0, width, height, NewColor(255, 0, 0 ));
	//rect_center(x0, y0, width, height, NewColor(0, 255, 0 ));
	
	DrawText( text, x0, y0, width, height, valid( color ) ? color.rgb() : COLOR_RGB( 255, 0, 0 ), FONT_FALLOUT, flags );
	
	if( underline )
		line( x0, y0 + 10, x0 + width, y0 + 10, NewColor( 255, 100, 100 ) );
}

void triangle( Dot@ dot, int size = 2 ) 
{ 
	if( !valid( dot ) || !valid( dot.color ) ) return;
	
	triangle( dot.x, dot.y, dot.color, size ); 
}

void triangle( int x, int y, Color color, int size = 2 )
{
	NewLine( "", color )
		.add( -size,  size )
		.add(  size,  size )
		.add(     0, -size )
		.draw( x, y, true );
}

void rect( int x, int y, int w, int h, Color color)
{
	NewLine( "", color )
		.add( 0, 0 )
		.add( w, 0 )
		.add( w, h)
		.add( 0, h)
		.draw( x, y, true );
}

void rect_solid( int x, int y, int w, int h, Color color, float alpha)
{
	uint8 alpha_byte;
	if(alpha >= 1.0) {
		alpha_byte = 0xFF;
	} else if(alpha <= 0.0) {
		alpha_byte = 0;
	} else {
		alpha_byte = uint8(alpha * 255.0);
	}
	int rgba = color.rgba(alpha_byte);

	int[]  drawData = { x, y, rgba, x + w, y, rgba, x, y + h, rgba, x + w, y + h, rgba };
	DrawPrimitive( DRAW_PRIMITIVE_TRIANGLESTRIP, drawData );
}

void rect_center( int x, int y, int w, int h, Color color)
{
	int half_w = w/2;
	int half_h = h/2;
	NewLine( "", color )
		.add( -half_w, -half_h )
		.add( half_w, -half_h )
		.add( half_w, half_h)
		.add( -half_w, half_h)
		.draw( x, y, true );
}

void line( int x1, int y1, int x2, int y2, Color color )
{
	NewLine( "", color ).add( x1, y1 ).add( x2, y2 ).draw();
}

void line( Dot@ a, Dot@ b, Color color )
{
	NewLine( "", color ).add(a).add(b).draw();
}

void DrawLine( Line@ line, int x0 = 0, int y0 = 0, float scale = 1, bool looped = false )
{
	if( !valid( line ) || !valid( line.dots ) || line.dots.length() == 0 )
		return;

	int[] drawData;
	
	for( uint i = 0, l = line.dots.length(); i <= l; i++ )
	{
		if( !looped && i == l ) break;
		
		Dot@ dot = line.dot(i%l);

		drawData.insertLast( x0 + dot.x * scale );
		drawData.insertLast( y0 + dot.y * scale );
	
		drawData.insertLast( dot.color.inherited ? line.color.rgb() : dot.color.rgb() );
	}
	
	DrawPrimitive( DRAW_PRIMITIVE_LINESTRIP, drawData );
	
	return;
}